buildscript {
    ext {
        kotlinVersion = '1.3.61'
        wpiLibVersion = "2020.2.2"
        junit5Version = "5.5.2"
        gdxVersion = '1.9.10'
        box2DLightsVersion = '1.4'
        roboSimVersion = "b7deee8225"
        abstractControllerLibVersion = "v2.3.2"
        abstractDashboardVersion = "e635c7080d"
        jacksonVersion = "2.10.1"
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
        google()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

subprojects {
    apply plugin: "kotlin"
    ext { // some of the variables in here may only be specific to a single module
        appName = "robot2020-sim-app"
        gdxVersion = "1.9.10"
    }
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        google()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://jitpack.io" }
        maven { url "https://frcmaven.wpi.edu/artifactory/release" }
    }
    dependencies {
        api "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5Version"
    }
    test {
        useJUnitPlatform()
    }
    compileKotlin {
        kotlinOptions {
            jvmTarget = "11"
        }
    }
}
project(":core"){
    apply plugin: "java-library"
}
project(":gdx"){
    apply plugin: "java-library"
    version = "1.0"
    dependencies {
        api project(":core") // if we change this to 'implementation', the desktop:dist jar produced stops working...
    }
}
project(":wpi"){
    apply plugin: "java"
    dependencies {
        api project(":core")
    }
}

project(":desktop") {
    apply plugin: "java-library"

    dependencies {
        api project(":gdx")
    }
}

wrapper {
    gradleVersion = '6.1'
    distributionType = Wrapper.DistributionType.ALL
}
